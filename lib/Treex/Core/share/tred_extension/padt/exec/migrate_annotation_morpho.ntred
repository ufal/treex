# -*- perl -*- ############################################################# Otakar Smrz, 2004/07/17
#
# migrate_annotation_morpho.ntred ####################################################### 2004/07/21

# $Id: migrate_annotation_morpho.ntred 4580 2011-04-27 23:29:49Z smrz $

our $VERSION = do { q $Revision: 4580 $ =~ /(\d+)/; sprintf "%4.2f", $1 / 100 };


package TredMacro;

use Algorithm::Diff;


our $entities;
our $nodes;


sub autostart {

    my $anno = Treex::PML::Instance->load({ 'filename' => FileName() . '.anno.xml' })->convert_to_fsfile();;

    $entities = [ [], [] ];

    @{$entities->[0]} = grep { $_->{'#name'} eq 'Element' } $grp->{FSFile}->trees();
    @{$entities->[1]} = grep { $_->{'#name'} eq 'Element' } $anno->trees();

    Algorithm::Diff::traverse_sequences(@{$entities}, { 'MATCH' => \&element_annotation }, \&element_ident);

    ChangingFile(1);
}


sub element_annotation {

    return if $entities->[0][$_[0]]->{'apply'} > 0;

    $nodes = [ [ $entities->[0][$_[0]] ], [ $entities->[1][$_[1]] ] ];

    migrate_annotation(0, 0);
}


sub element_ident {

    return $_[0]->{'input'};
}


sub migrate_annotation {

    $this = $nodes->[0][$_[0]];
    $root = $nodes->[0][$_[0]]->root();

    if (defined $nodes->[1][$_[1]]->{'restrict'} and $nodes->[1][$_[1]]->{'restrict'} ne '') {

        PADT::Morpho::restrict_hide($nodes->[1][$_[1]]->{'restrict'}, $nodes->[1][$_[1]]->{'inherit'} eq '' &&
                                                                     $nodes->[1][$_[1]]->{'#name'} ne 'Element' ? 'remove inherited' : () );
    }
    elsif ($nodes->[1][$_[1]]->{'#name'} eq 'Token' and $nodes->[1][$_[1]]->{'apply'} > 0) {

        PADT::Morpho::annotate_morphology('click') unless $nodes->[0][$_[0]]->{'apply'} > 0;
    }

    $nodes->[0][$_[0]]->{'note'} = $nodes->[1][$_[1]]->{'note'} if $nodes->[0][$_[0]]->{'note'} eq '';

    local $nodes = [ [ $nodes->[0][$_[0]]->children() ], [ $nodes->[1][$_[1]]->children() ] ];

    Algorithm::Diff::traverse_sequences(@{$nodes}, { 'MATCH' => \&migrate_annotation }, \&node_ident);
}


sub node_ident {

    return join '|', @{$_[0]}{'form', 'tag', $_[0]->{'#name'} eq 'Element' ? () : ( '#name', 'id' ) };
}
